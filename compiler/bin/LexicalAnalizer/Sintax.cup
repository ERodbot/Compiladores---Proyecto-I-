package LexicalAnalizer;
import java_cup.runtime.*;



/**
    * Function calls
    * 
    * 
    * 
*/
action code {:
    public void uno(){
        System.out.println("123");
    }
    public void dos(){
        System.out.println("456");
    }
:}


/**
    * create parser constructor, assigns to self lexer and initializes symbol factory
    * 
    * 
    * 
*/
parser code {:
  LexerCup lex;
  @SuppressWarnings("deprecation")
  public Sintax(LexerCup lex){
    this.lex=lex;
    this.symbolFactory= new DefaultSymbolFactory();
  }
:}

init with {: :};

scan with {: return lex.next_token(); :};

// Terminals

// Data Types
terminal NOEL, NICOLAS, SANTA, COLACHO, CLAUS;

//Parenthesis
terminal ABRECUENTO, CIERRACUENTO, ABREEMPAQUE, CIERRAEMPAQUE, ABREREGALO, CIERRAREGALO;

//Separator
terminal CHIMENEA;

// Control Structures
terminal ELFO, HADA, DUENDE, ENVUELVE, HACE, REVISA, ENVIA, CORTA;

//Literals numbers
terminal l_NICOLAS, l_NOEL;

//Literal strings
terminal l_SANTA;

//Literal char
terminal l_COLACHO;

// Input/Output Functions
terminal NARRA, ESCUCHA;

//null
terminal NARIZROJA;

//void 
terminal SINREGALO;

// Relational Operators
terminal ALABASTRO, EVERGEEN, UPATREE, MINSTIX, MARY, OPENSLAE;

// Logical Operators
terminal GASPAR, MELCHOR, BALTASAR;

// Assignment
terminal ENTREGA;

// Boolean Literals
terminal l_t_CLAUS, l_f_CLAUS;

// Unary Operators
terminal GRINCH, QUIEN;

// Binary Arithmetic Operators
terminal RODOLFO, TRUENO, RELAMPAGO, JUGUETON, COMETA, CUPIDO;

// Identifier
terminal PERSONA;

//Function
terminal RECORRIDO;

// End of Line
terminal FINREGALO;

// Lexical Error
terminal ERRORNOTRECOGNIZED;

//no terminal 
non terminal   main, nevada_external_declaration, saco_function_definition, arbol_constant, gorro_navidad_type_specifier, risa_santa, lazo_io_statement, belen_function_call_statement,
               calcetin_compound_statement, empaque_block_item_list, carta_block_item, deseos_selection_statement, 
			   tradicion_elif_statement, caramelo_iteration_statement, calcetin_jump_statement, nochebuena_initializer, pino_initializer_list, campanas_initializer_list_integer,
			   guirnaldas_initializer_list_char, estrella_assign_array, candelabro_assignment_expression, pesebre_args, reyes_magicos_list_expr, incienzo_expression, bastoncillo_arithmetic_expr, galleta_arithmetic_expr, 
			   leche_relational_expr;

// Precedence and associativity

precedence left TRUENO, RODOLFO; //resta
precedence left JUGUETON, RELAMPAGO;  //division, multiplicacion
precedence left CUPIDO, COMETA; // mod, exponente
precedence left QUIEN, GRINCH; // Increment and decrement
precedence left ALABASTRO, EVERGEEN;
precedence left MINSTIX, UPATREE, OPENSLAE, MARY;
precedence left ABRECUENTO, CIERRACUENTO;	
precedence left GASPAR;
precedence left MELCHOR; 
precedence left BALTASAR;


start with main;

main ::= nevada_external_declaration
	| main nevada_external_declaration
	;


nevada_external_declaration ::= saco_function_definition | risa_santa; 


saco_function_definition ::= gorro_navidad_type_specifier RECORRIDO PERSONA ABRECUENTO pesebre_args CIERRACUENTO calcetin_compound_statement FINREGALO
    | gorro_navidad_type_specifier RECORRIDO PERSONA ABRECUENTO  CIERRACUENTO calcetin_compound_statement FINREGALO;


arbol_constant ::= l_NOEL	| l_NICOLAS | l_COLACHO | l_SANTA | l_t_CLAUS | l_f_CLAUS | NARIZROJA;


gorro_navidad_type_specifier ::= SINREGALO
  | COLACHO
  | SANTA
  | NOEL
  | NICOLAS
  | CLAUS
  ;


risa_santa ::= calcetin_compound_statement
	| candelabro_assignment_expression
	| deseos_selection_statement
	| caramelo_iteration_statement
	| calcetin_jump_statement
    | lazo_io_statement
	| belen_function_call_statement
	;

lazo_io_statement ::= NARRA ABRECUENTO l_SANTA CIERRACUENTO FINREGALO
  | ESCUCHA ABRECUENTO l_SANTA CIERRACUENTO FINREGALO 
  ;

belen_function_call_statement ::= PERSONA ABRECUENTO reyes_magicos_list_expr CIERRACUENTO FINREGALO;

calcetin_compound_statement ::= ABREREGALO CIERRAREGALO
	| ABREREGALO empaque_block_item_list CIERRAREGALO
	;

empaque_block_item_list ::= carta_block_item | empaque_block_item_list carta_block_item;

carta_block_item ::= risa_santa;
	
deseos_selection_statement ::= ELFO ABRECUENTO incienzo_expression CIERRACUENTO calcetin_compound_statement FINREGALO                                             // if () {};
	| ELFO ABRECUENTO incienzo_expression CIERRACUENTO calcetin_compound_statement DUENDE calcetin_compound_statement FINREGALO                             // if () {} else {};
    | ELFO ABRECUENTO incienzo_expression CIERRACUENTO calcetin_compound_statement tradicion_elif_statement DUENDE calcetin_compound_statement FINREGALO           // if () {} elif () {} else() {};
	;

tradicion_elif_statement ::=  HADA  ABRECUENTO incienzo_expression CIERRACUENTO calcetin_compound_statement
	| tradicion_elif_statement HADA  ABRECUENTO incienzo_expression CIERRACUENTO calcetin_compound_statement;


caramelo_iteration_statement ::= HACE risa_santa REVISA ABRECUENTO incienzo_expression CIERRACUENTO FINREGALO
	| ENVUELVE ABRECUENTO candelabro_assignment_expression CHIMENEA incienzo_expression CHIMENEA incienzo_expression CIERRACUENTO risa_santa FINREGALO 
	;

calcetin_jump_statement ::= CORTA FINREGALO
	| ENVIA FINREGALO
	| ENVIA incienzo_expression FINREGALO
	;

nochebuena_initializer ::=  ABREREGALO pino_initializer_list CIERRAREGALO
    | ABREREGALO CIERRAREGALO ;

pino_initializer_list ::= campanas_initializer_list_integer | guirnaldas_initializer_list_char;

campanas_initializer_list_integer ::= l_NOEL | campanas_initializer_list_integer CHIMENEA l_NOEL ;

guirnaldas_initializer_list_char ::= l_COLACHO | guirnaldas_initializer_list_char CHIMENEA l_COLACHO ;

estrella_assign_array ::= gorro_navidad_type_specifier PERSONA ABREEMPAQUE CIERRAEMPAQUE 
	| gorro_navidad_type_specifier ABREEMPAQUE CIERRAEMPAQUE
	| gorro_navidad_type_specifier PERSONA ABREEMPAQUE l_NOEL CIERRAEMPAQUE  
	| gorro_navidad_type_specifier ABREEMPAQUE l_NOEL CIERRAEMPAQUE
	;

candelabro_assignment_expression ::= gorro_navidad_type_specifier PERSONA ENTREGA incienzo_expression FINREGALO
	| estrella_assign_array FINREGALO
	| estrella_assign_array ENTREGA nochebuena_initializer FINREGALO
	| estrella_assign_array ENTREGA incienzo_expression FINREGALO
	;

pesebre_args ::= gorro_navidad_type_specifier PERSONA | pesebre_args CHIMENEA gorro_navidad_type_specifier PERSONA;


reyes_magicos_list_expr ::= incienzo_expression | reyes_magicos_list_expr CHIMENEA incienzo_expression;


incienzo_expression ::= bastoncillo_arithmetic_expr
       | galleta_arithmetic_expr
       | leche_relational_expr
       | arbol_constant
       | PERSONA  // Identificador
       | belen_function_call_statement
       ;


// Expresiones aritméticas
bastoncillo_arithmetic_expr ::= incienzo_expression TRUENO incienzo_expression // Resta 
    | incienzo_expression RODOLFO incienzo_expression // Suma
    | incienzo_expression JUGUETON incienzo_expression // División 
    | incienzo_expression RELAMPAGO incienzo_expression // Multiplicación
    | incienzo_expression CUPIDO incienzo_expression // Modulo
    | incienzo_expression COMETA incienzo_expression //Exponente
    | QUIEN PERSONA %prec QUIEN // Incremento prefijo
    | GRINCH PERSONA %prec GRINCH // Decremento prefijo
    | ABRECUENTO incienzo_expression CIERRACUENTO // Expresiones entre paréntesis
	;

// Expresiones lógicas
galleta_arithmetic_expr ::= incienzo_expression GASPAR incienzo_expression // Y lógico
    | incienzo_expression MELCHOR incienzo_expression // O lógico
    | BALTASAR incienzo_expression // Negación lógica
	;

// Expresiones relacionales
leche_relational_expr ::= incienzo_expression ALABASTRO incienzo_expression // Igual
    | incienzo_expression EVERGEEN incienzo_expression // No igual
    | incienzo_expression MINSTIX incienzo_expression // Menor que
    | incienzo_expression UPATREE incienzo_expression // Mayor que
    | incienzo_expression OPENSLAE incienzo_expression // Menor o igual
    | incienzo_expression MARY incienzo_expression // Mayor o igual
    ;


